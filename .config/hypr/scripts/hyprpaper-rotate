#!/bin/zsh

set -euo pipefail

# NOTE: this scripts will set the same wallpaper on all monitors.

# Get a list of all wallpapers
wallpapers_dir="${WALLPAPERS_HOME:-${XDG_DATA_HOME:-$HOME/.local/share}/wallpapers}"
wallpapers=( $(fd --type f . "$wallpapers_dir") )

# Gets the active wallpaper
current="$(hyprctl hyprpaper listactive | grep -oP '(?<= = ).*' | uniq)"

# Gets the wallpaper index, increment it, and check for array overflow
(( current_idx = ${wallpapers[(ie)$current]}, next_idx = current_idx + 1 ))
[[ $next_idx -gt ${#wallpapers[@]} ]] && (( next_idx = 1 ))

# Apply the the next wallpaper in the list
wallpaper="${wallpapers[$next_idx]}"
hyprctl hyprpaper reload ,"$wallpaper"

# Updates ~/.config/hypr/hyprpaper.conf with currently active wallpaper
cat <<EOL >! ~/.config/hypr/hyprpaper.conf
preload = ${wallpaper}
wallpaper = , ${wallpaper}
EOL
