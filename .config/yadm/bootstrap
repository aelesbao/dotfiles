#!/usr/bin/env zsh

set -euo pipefail


export GITHUB_USER="${GITHUB_USER:-aelesbao}"
export BOOTSTRAP_D="${0:A}.d"


function _log() {
  local level="${1}"
  local message="${@:$#}"
  printf "$level %s\033[0m\n" "$message"
}

function info() {
  _log "\033[1;37m∷" "${@:$#}"
}

function notice() {
  _log "\033[0;34m⁚" "${@:$#}"
}

function success() {
 _log "\033[0;32m⁚" "${@:$#}"
}

function warn() {
  _log "\033[0;33m∴" "${@:$#}"
}

function error() {
  _log "\033[0;31m⁘" "${@:$#}"
}

function msg() {
  _log "\033[0;37m⁚" "${@:$#}"
}

function fail() {
  error "${@}" >&2
  exit 1
}

function ask() {
  printf "\033[0;36m⁖ ${@:$#}\033[0m [y/n] ‣ "
  if read -q "choice?"; then
    echo
    return 0
  else
    echo
    return 1
  fi
}

function result() {
  local result="$?"
  local message="${@:$#}"

  if [[ "${result}" == "0" ]]; then
    success "[✔] ${message} succeeded"
  else
    error "[✖] ${message} failed"
  fi
}

function title() {
  echo
  echo -e "\033[1;35m≡ ${@:$#}\033[0m"
}

function has-command() {
  command -v "$@" >/dev/null 2>&1
}

function is-macos() {
  [[ "$OSTYPE" == darwin* ]]
}

function is-ubuntu() {
  has-command lsb_release && lsb_release -d | awk -F"\t" '{print $2}' | grep -iq "Ubuntu"
}

function is-arch() {
  has-command pacman
}

function is-pkg-installed() {
  local pkg="${1}"

  if is-macos; then
    { brew list --formula "$pkg" || brew list --cask "$pkg" } >/dev/null 2>&1
  elif has-command dpkg-query; then
    dpkg-query -W "$pkg" >/dev/null 2>&1
  else
    fail "Unsupported OS: $(uname -a)"
  fi
}

function pkg-install() {
  local pkg="${1}"
  msg "Installing $pkg"

  if is-macos; then
    brew install $pkg
  elif has-command apt; then
    sudo apt install -y $pkg
  elif has-command pacman; then
    sudo pacman -S --noconfirm $pkg
  else
    fail "Unsupported OS: $(uname -a)"
  fi
}

function require() {
  local pkg="${1}"
  is-pkg-installed "$pkg" || \
    pkg-install "$pkg" || \
    fail "Failed to install $pkg"
}

function get-latest-tag() {
  local repo="${1}"
  curl -fsSL "https://api.github.com/repos/${repo}/tags" | jq -r ".[0].name"
}


cd "$HOME"

if [[ ! -d "$BOOTSTRAP_D" ]]; then
  fail "bootstrap scripts directory $BOOTSTRAP_D not found"
fi

declare scripts_filter
scripts_filter="*(${(j:.zsh|:)@}.zsh)*"

declare -a bootstrap_scripts
bootstrap_scripts=($(find "$BOOTSTRAP_D" -type f -name '*.zsh' | sort))
bootstrap_scripts=(${(M)bootstrap_scripts:#${~scripts_filter}})

title "Running bootstrap scripts"
for bootstrap in ${bootstrap_scripts[@]}; do
  if [[ -x "$bootstrap" && ! "$bootstrap" =~ "##" && ! "$bootstrap" =~ "~$" ]]; then
      echo
      notice "bootstrapping ${bootstrap:t:r}..."
      {
        source "$bootstrap"
      } || false
      result "bootstrap ${bootstrap:t:r}"
  fi
done
